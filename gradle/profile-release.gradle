/*
 * Copyright 2026 Karlsruhe Institute of Technology.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
////////////////////////////////////////////////////////////////////////////////
// Version tags start with 'v' e.g. v1.0.0
// codemeta.json and CITATION.cff are updated before release
////////////////////////////////////////////////////////////////////////////////
import java.time.LocalDate
import java.time.format.DateTimeFormatter
import groovy.json.JsonSlurper
import groovy.json.JsonOutput


def todayDate = LocalDate.now().format(DateTimeFormatter.ISO_DATE)

////////////////////////////////////////////////////////////////////////////////
//for plugin net.researchgate.release
//see https://github.com/researchgate/gradle-release
////////////////////////////////////////////////////////////////////////////////
release {
    //define template for tagging, e.g. v1.0.0
    tagTemplate = 'v${version}'
    //set source file of version property
    versionPropertyFile = 'gradle.properties'
    //set possible properties which may contain the version
    versionProperties = ['version', 'mainversion']
    git {
        //branch from where to release (default: main)
        requireBranch.set('main')
    }
}

////////////////////////////////////////////////////////////////////////////////
// update CITATION.cff file (just date and version)
////////////////////////////////////////////////////////////////////////////////
tasks.register('updateCitationFile') {
    doLast {
        def citationFile = file('CITATION.cff')
        def lines = citationFile.readLines()
        def map = ['version': project.version, 'date-released': todayDate]
        map.each { key, value ->
            lines = lines.collect { line ->
                if (line.trim().startsWith("${key}:")) {
                    return "${key}: ${value}"
                } else {
                    return line
                }
            }
        }
        citationFile.text = lines.join(System.lineSeparator())
        println "Updated CITATION.cff with version ${project.version} and date ${todayDate}"
    }
}
////////////////////////////////////////////////////////////////////////////////
// update codemeta.json file (just date and version)
////////////////////////////////////////////////////////////////////////////////
tasks.register('updateCodemetaFile') {
    doLast {
        def codemetaFile = file('codemeta.json')
        def codemeta = new JsonSlurper().parseText(codemetaFile.text)
        codemeta.version = project.version
        codemeta.softwareVersion = project.version
        codemeta.dateModified = todayDate

        codemetaFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(codemeta))
        println "Updated codemeta.json with (software)version ${project.version} and date ${todayDate}"
    }
}
////////////////////////////////////////////////////////////////////////////////
// Hook the update tasks into the release process
////////////////////////////////////////////////////////////////////////////////
tasks.named('beforeReleaseBuild') {
    dependsOn 'updateCitationFile', 'updateCodemetaFile'
}
