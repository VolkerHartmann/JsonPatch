plugins {
    id 'java-library'
    // plugins for release and publishing to maven repo
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'maven-publish'
}

import java.time.LocalDate
import java.time.format.DateTimeFormatter
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

group = 'edu.kit.datamanager'


println "Building ${name} version: ${version}"
println "Running gradle version: $gradle.gradleVersion"
println "JDK version: ${JavaVersion.current()}"

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.9'

    api 'tools.jackson.core:jackson-databind:3.0.3'
    api 'jakarta.json:jakarta.json-api:2.0.1'

    api 'jakarta.json:jakarta.json-api:2.1.3'         // API definition
//    implementation 'org.glassfish:jakarta.json:2.0.1' // reference implementation

    implementation 'org.eclipse.parsson:parsson:1.1.4'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
}

def todayDate = LocalDate.now().format(DateTimeFormatter.ISO_DATE)

// update CITATION.cff file (just date and version)
tasks.register('updateCitationFile') {
    doLast {
        def citationFile = file('CITATION.cff')
        def lines = citationFile.readLines()
        def map = ['version': project.version, 'date-released': todayDate]
        map.each { key, value ->
            lines = lines.collect { line ->
                if (line.trim().startsWith("${key}:")) {
                    return "${key}: ${value}"
                } else {
                    return line
                }
            }
        }
        citationFile.text = lines.join(System.lineSeparator())
        println "Updated CITATION.cff with version ${project.version} and date ${todayDate}"
    }
}
// update codemeta.json file (just date and version)
tasks.register('updateCodemetaFile') {
    doLast {
        def codemetaFile = file('codemeta.json')
        def codemeta = new JsonSlurper().parseText(codemetaFile.text)
        codemeta.version = project.version
        codemeta.softwareVersion = project.version
        codemeta.dateModified = todayDate

        codemetaFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(codemeta))
        println "Updated codemeta.json with (software)version ${project.version} and date ${todayDate}"
    }
}
// Hook the update tasks into the release process
tasks.named('beforeReleaseBuild') {
    dependsOn 'updateCitationFile', 'updateCodemetaFile'
}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'JsonPatch'
        }
    }
    repositories {
        mavenLocal()

    }
}
