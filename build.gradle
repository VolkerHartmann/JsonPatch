plugins {
    id 'java-library'
    // plugins for release and publishing to maven repo
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'maven-publish'
    id 'jacoco'
}

description = 'A small utility library for applying JSON Patches to JSON documents.'
group = 'edu.kit.datamanager'


println "Building '${name}' version: '${version}'"
println "Running gradle version: $gradle.gradleVersion"
println "JDK version: ${JavaVersion.current()}"

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

def buildProfile = project.findProperty("buildProfile") ?: "minimal"

def profileFile = file("$rootDir/gradle/profile-${buildProfile}.gradle")

if (profileFile.exists()) {
    apply from: profileFile
} else {
    throw new GradleException("Profile-file missing: $profileFile")
}

dependencies {
    implementation 'org.eclipse.parsson:parsson:1.1.4'
    implementation 'org.slf4j:slf4j-api:2.0.9'

    api 'jakarta.json:jakarta.json-api:2.1.3'         // API definition
    api 'tools.jackson.core:jackson-databind:3.0.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
}
/////////////////////////////////////////////////////////////////////////////////
// Code coverage configuration with JaCoCo
/////////////////////////////////////////////////////////////////////////////////
test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.14"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }
}

///////////////////////////////////////////////////////////////////////////////
//for plugin maven-publish
//see https://docs.gradle.org/current/userguide/publishing_maven.html
///////////////////////////////////////////////////////////////////////////////
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'json-patch-util'
            version = project.version
            pom {
                name = 'JSON Patch Utility Library'
                description = 'A small utility library for applying JSON Patches to JSON documents.'
                url = 'https://datamanager.kit.edu'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Jejkal'
                        name = 'Thomas Jejkal'
                        email = 'webmaster@datamanager.kit.edu'
                        roles = ['developer', 'maintainer']
                    }
                    developer {
                        id = 'HartmannV'
                        name = 'Volker Hartmann'
                        roles = ['developer', 'maintainer']
                    }
                }
                scm {
                    connection = 'scm:git:github.com/kit-data-manager/JsonPatch'
                    developerConnection = 'scm:git:github.com/kit-data-manager/JsonPatch'
                    url = 'https://github.com/kit-data-manager/JsonPatch'
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}
